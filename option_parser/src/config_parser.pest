config_statement = { 
    "CONFIG_" ~ all_config_statement
}
    all_config_statement = _{config_platform | config_debug | config_ignore}

    config_debug = {
        "DEBUG_" ~ all_config_debug
    }
        all_config_debug = _{
            debug_rl_history_size |
            debug_default
        }

        debug_rl_history_size = _{
            "RL_HISTORY_SIZE" ~ "=" ~ rl_history_size
        }
            rl_history_size = { config_value_dec }

        debug_default = _{
            "DEFAULT_" ~ all_default
        }

            all_default = _{
                default_itrace_enable |
                default_wave_trace_enable
            }

            default_itrace_enable = _{
                "ITRACE_ENABLE" ~ "=" ~ itrace_enable
            }
                itrace_enable = { config_value_bool }

            default_wave_trace_enable = _{
                "WaveTRACE_ENABLE" ~ "=" ~ wave_trace_enable
            }
                wave_trace_enable = { config_value_bool }

    config_platform = {
        platform ~ "_" ~ target ~ "_" ~ target_config
    }
        platform = { ASCII_ALPHA+ }

        target = { ASCII_ALPHA+ }

        target_config = _{
            target_base |
            target_region |
            target_cache
        }

        target_base = {
            "BASE_" ~ target_base_resetVector
        }

            target_base_resetVector = _{
                "RESET_VECTOR" ~ "=" ~ "0x" ~ reset_vector_value
            }

            reset_vector_value = { config_value_hex }

        target_region  = {
            ("MEM_" ~ target_mem_region ~ "=" ~ region_value) |
            ("DEV_" ~ target_dev_region ~ "=" ~ region_value)
        }

            target_mem_region = { ASCII_ALPHA+ }

            target_dev_region = { ASCII_ALPHA+ }

            region_value = _{
                "\"" ~ "0x" ~ region_base ~ " " ~ "0x" ~ region_size ~ " " ~ "0b" ~ region_flag ~ "\""
            }
                region_base = { config_value_hex }
                region_size = { config_value_hex }
                region_flag = { config_value_bin }
        
        target_cache = {
            ("Cache_" ~ target_cache_name ~ "=" ~ target_cache_config)
        }

            target_cache_name = { ASCII_ALPHA+ }

            target_cache_config = _{
                "\"" ~ target_cache_set ~ " " ~ target_cache_way ~ " " ~ target_cache_blocknum ~ " " ~ target_cache_replacement ~ "\""
            }
                target_cache_set = { config_value_dec }
                target_cache_way = { config_value_dec }
                target_cache_blocknum = { config_value_dec }
                target_cache_replacement = { "lru" | "plru" }

    config_value_bool = _{ "y" }
    config_value_hex  = _{ASCII_HEX_DIGIT+}
    config_value_bin  = _{ASCII_BIN_DIGIT+}
    config_value_dec  = _{ASCII_DIGIT+}

    config_ignore = {
        (ASCII_ALPHA+ ~ ("_" ~ ASCII_ALPHA+)*) ~ "=" ~ config_value_bool
    }

comment_statement = _{ "#" ~ (!NEWLINE ~ ANY)* }

line = _{ ((config_statement | comment_statement) ~ NEWLINE) | NEWLINE }

// 文件规则
file = _{ 
    SOI ~
    (line)*
    ~ EOI
}
